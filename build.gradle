plugins {
  id 'org.springframework.boot' version '2.5.5'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'application'
  id 'com.adarshr.test-logger' version '3.0.0'
  id 'org.liquibase.gradle' version '2.0.4'
  id 'jacoco'
  id "org.springdoc.openapi-gradle-plugin" version "1.3.3"
  id "com.github.johnrengelman.processes" version "0.5.0"
}

application {
  mainClass = 'exercise.App'
}

group = 'exercise'

repositories {
  mavenCentral()
}

compileJava {
  compileJava.options.encoding = "UTF-8"
  options.release = 16
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.apache.commons:commons-lang3:3.12.0'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'com.h2database:h2:1.4.200'
  implementation 'org.liquibase:liquibase-core'
  compileOnly 'org.projectlombok:lombok'
  annotationProcessor 'org.projectlombok:lombok'
  testImplementation 'com.github.database-rider:rider-junit5:1.32.0'

  liquibaseRuntime 'org.liquibase:liquibase-core:3.8.1'
  liquibaseRuntime 'org.springframework.boot:spring-boot-starter-data-jpa'
  liquibaseRuntime 'org.liquibase.ext:liquibase-hibernate5:3.6'
  liquibaseRuntime sourceSets.main.output

  // https://mvnrepository.com/artifact/org.springdoc/springdoc-openapi-ui
  implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.5.12'
}

test {
  useJUnitPlatform()
}

testlogger {
  showStandardStreams = true
}

openApi {
  apiDocsUrl.set("http://localhost:5000/v3/api-docs")
}

diffChangeLog {
  dependsOn compileJava
}

jacocoTestReport {
  reports {
    xml.required = true
  }
}
liquibase {
  activities {
    main {
      changeLogFile 'src/main/resources/db/changelog/changelog-master.xml'
      url 'jdbc:h2:./hexlet'
      username 'sa'
      password ''
      referenceUrl 'hibernate:spring:exercise.model' +
              '?dialect=org.hibernate.dialect.H2Dialect' +
              '&hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy'
    }
  }
}
jar {
  baseName = 'heroku-gradle-spring-boot'
}
installDist.mustRunAfter clean

